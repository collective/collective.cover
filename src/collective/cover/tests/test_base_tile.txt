*** Settings ***

Library  Selenium2Library  timeout=5 seconds  implicit_wait=3 seconds
Resource  keywords.txt
Resource  cover_keywords.txt
Variables  plone/app/testing/interfaces.py

Suite Setup  Start Browser and Log In
Suite Teardown  Close Browser

*** Variables ***

${basic_tile_location}  'collective.cover.basic'
${basic_uuid}  12345
${tile_selector}  div.tile-container div.tile
${default_tile}  div.tile-default
${border_tile}  div.tile-edge
${border_class}  tile-edge
${tile_class}  div.cover-tile

*** Test cases ***

Test basic Tile
    # XXX: should we create the cover object programmatically?
    Create Cover  Title  Description  Empty layout
    Click Link  link=Layout

    Add Tile  ${basic_tile_location}
    Save Cover Layout

    # now we move to the default view to check the default tile style
    Click Link  link=View
    Log Source
    Page Should Contain Element  css=${default_tile}

    # and now change style configuration
    # start with default style
    Click Link  link=Layout
    Click Config from Tile  ${tile_class}
    Click Button  Save
    Save Cover Layout

    # go to View page and css remains
    Click Link  link=View
    Page Should Contain Element  css=${default_tile}

    # change style
    Click Link  link=Layout
    Click Config from Tile  ${tile_class}
    Select From List  css=select#collective-cover-basic-css_class  ${border_class}
    Click Button  Save
    Save Cover Layout

    # go to View page and check new style
    Click Link  link=View
    Page Should Contain Element  css=${border_tile}


*** Keywords ***
Click Config from Tile
    [arguments]  ${tile}

    Click Element  css=${tile} .config-tile-link

